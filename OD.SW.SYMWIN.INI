[===============================================================================
SPECFILENAME: OD.SW.SYMWIN.INI

SHORTDESC:  
     A well organized HTML menu of on-demand specfiles to be run from the Account Manager and Teller Transactions work areas.
DETAILS: 

REVISIONDATE: February 2012
PROGRAMMER:   Stanley Weschler, FedChoice FCU
REVSUMMARY:   Initial program creation

REVISIONDATE: March 2024
PROGRAMMER:   Alex Carlston,Great NorthWest FCU
REVSUMMARY:   Changed stylesheet location to public API calls

INITIATEDBY:  Demand
RUNINFO:      Run from Account Manager/Teller Transactions Specfile drop-down.

INSTALL:      This specfile must be installed for demand use.
PREREQ:       jQuery/jQuery UI must exist on local machine or accessible network location.

SPECIALKEY:   WINDOWS
TARGETFILE:   ACCOUNT
SORT:         None
SUBTOTALS:    None
TOTALS:       None
RPTCATEGORY:  Demand
OUTPUTTITLE:  N/A
OUTPUTFORMAT: HTML
OUTPUTFILES:  None
INPUTFILES:   SW.SYMWIN.INI from letter file directory
INCLUDEFILES: None
SUBROUTINES:  Subroutines called from letterfile listing
===============================================================================]
SUBROUTINE DEMAND WINDOWS
TARGET=ACCOUNT

DEFINE
 Q=CHARACTER
 TMP=CHARACTER
 FNAMEALLSETTINGS=CHARACTER
 FNOALLSETTINGS=NUMBER
 FLINE=CHARACTER
 FERR=CHARACTER
 SUBNAME=CHARACTER
 SUBERR=CHARACTER
 X=NUMBER
 Y=NUMBER
 CATEGORY=CHARACTER ARRAY(30,1)[cat | cat title]
 RGNAMES=CHARACTER ARRAY(30,100,1)[cat | rgCt | rg,rgText]
 CAT=NUMBER
 RG=NUMBER
 DEPTTAB=CHARACTER ARRAY(99)
END

SETUP
 DEPTTAB(10)="SW.SYMWIN.ACCTG.INI"
 Q=CTRLCHR(34)
 FNAMEALLSETTINGS="SW.SYMWIN.INI"
 FILEOPEN("LETTER",FNAMEALLSETTINGS,"READ",FNOALLSETTINGS,FERR)
 CALL READALLSETTINGS
 IF DEPTTAB(GETDATANUMBER(1,SYSCONSOLENUM))<>"" THEN
  DO
   FNAMEALLSETTINGS=DEPTTAB(GETDATANUMBER(1,SYSCONSOLENUM))
   FILEOPEN("LETTER",FNAMEALLSETTINGS,"READ",FNOALLSETTINGS,FERR)
   CALL READALLSETTINGS
   FNAMEALLSETTINGS="SW.SYMWIN.INI"
  END
 CALL SHOWUSERMAINMENU
END

PRINT TITLE="ON DEMAND"
 SUPPRESSNEWLINE
END

PROCEDURE READALLSETTINGS
 FILEREADLINE(FNOALLSETTINGS,FLINE,FERR)
 WHILE FERR<>"EOF"
  DO
   IF CHARACTERSEARCH(FLINE,"<tabName>")>0 THEN
    DO
     CAT=CAT+1
     RG=0
     CATEGORY(CAT,0)=SEGMENT(FLINE,CHARACTERSEARCH(FLINE,"<tabName>")+9,CHARACTERSEARCH(FLINE,"</tabName>")-1)
    END
   IF CHARACTERSEARCH(FLINE,"<sectionHead>")>0 THEN
    DO
     RG=RG+1
     RGNAMES(CAT,RG,0)=SEGMENT(FLINE,CHARACTERSEARCH(FLINE,"<sectionHead>")+13,CHARACTERSEARCH(FLINE,"</sectionHead>")-1)
    END
   IF CHARACTERSEARCH(FLINE,"<specfile>")>0 THEN
    DO
     RG=RG+1
     RGNAMES(CAT,0,0)=FORMAT("99",RG)
     RGNAMES(CAT,RG,0)=SEGMENT(FLINE,CHARACTERSEARCH(FLINE,"<specfile>")+10,CHARACTERSEARCH(FLINE,"</specfile>")-1)
     FILEREADLINE(FNOALLSETTINGS,FLINE,FERR)
     RGNAMES(CAT,RG,1)=SEGMENT(FLINE,CHARACTERSEARCH(FLINE,"<text>")+6,CHARACTERSEARCH(FLINE,"</text>")-1)
    END
   FILEREADLINE(FNOALLSETTINGS,FLINE,FERR)
  END
 FILECLOSE(FNOALLSETTINGS,FERR)
END

PROCEDURE SHOWUSERMAINMENU
 HTMLVIEWOPEN
 HTMLVIEWLINE("<!DOCTYPE HTML>")
 HTMLVIEWLINE("<html>")
 HTMLVIEWLINE("<head>")
 HTMLVIEWLINE("<meta http-equiv="+Q+"X-UA-Compatible"+Q+" content="+Q+"IE=9"+Q+" />")
 HTMLVIEWLINE("<title>Main Menu</title>")
 HTMLVIEWLINE("<script type="+Q+"text/javascript"+Q+" src="+Q+"https://ajax.aspnetcdn.com/ajax/jQuery/jquery-3.6.4.min.js"+Q+"></script>")
 HTMLVIEWLINE("<script type="+Q+"text/javascript"+Q+" src="+Q+"https://ajax.aspnetcdn.com/ajax/jquery.ui/1.13.2/jquery-ui.min.js"+Q+"></script>")
 [HTMLVIEWLINE("<link type="+Q+"text/css"+Q+" href="+Q+"https://ajax.aspnetcdn.com/ajax/jquery.ui/1.13.2/themes/hot-sneaks/jquery-ui.css"+Q+" rel="+Q+"stylesheet"+Q+" />")]
 [HTMLVIEWLINE("<link type="+Q+"text/css"+Q+" href="+Q+"https://ajax.aspnetcdn.com/ajax/jquery.ui/1.13.1/themes/eggplant/jquery-ui.css"+Q+" rel="+Q+"stylesheet"+Q+" />")]
 HTMLVIEWLINE("<link type="+Q+"text/css"+Q+" href="+Q+"https://ajax.aspnetcdn.com/ajax/jquery.ui/1.13.1/themes/smoothness/jquery-ui.css"+Q+" rel="+Q+"stylesheet"+Q+" />")
 
 HTMLVIEWLINE("<script type='text/javascript'>")
 HTMLVIEWLINE("function sbmtRgName(rgName){")
 HTMLVIEWLINE(" var h = document.createElement('input');")
 HTMLVIEWLINE(" h.type = 'hidden';")
 HTMLVIEWLINE(" h.id = 'rgName';")
 HTMLVIEWLINE(" h.name = 'rgName';")
 HTMLVIEWLINE(" h.value = rgName;")
 HTMLVIEWLINE(" document.form1.appendChild(h);")
 HTMLVIEWLINE(" document.form1.submit();")
 HTMLVIEWLINE("}")

 HTMLVIEWLINE("$(document).ready(function(){")
 HTMLVIEWLINE("   $('#tabs').tabs();")

 HTMLVIEWLINE("   $('#autocomplete').autocomplete({ ")
 HTMLVIEWLINE("               source: ")
 HTMLVIEWLINE("[")
 FOR X=1 TO CAT
  DO
   FOR Y=1 TO VALUE(RGNAMES(X,0,0))
    DO
     IF RGNAMES(X,Y,1)<>"" THEN
      DO
       HTMLVIEWLINE("{"+Q+"value"+Q+":"+Q+RGNAMES(X,Y,1)+Q+","+Q+"id"+Q+":"+Q+rgnames(X,Y,0)+Q+"},")
      END
    END
  END
 HTMLVIEWLINE("]")
 HTMLVIEWLINE("}); ")

 HTMLVIEWLINE("   $('#autocomplete').autocomplete({")
 HTMLVIEWLINE("               select: function(event, ui) {sbmtRgName(ui.item.id); }")
 HTMLVIEWLINE("});")

 HTMLVIEWLINE("});")[doc.ready]

 HTMLVIEWLINE("</script>")

 HTMLVIEWLINE("<style type='text/css'>")
 HTMLVIEWLINE("body{")
 HTMLVIEWLINE(" font: 10px "+Q+"Trebuchet MS"+Q+", sans-serif;")
 HTMLVIEWLINE(" margin: 5px;")
 HTMLVIEWLINE("}")

 HTMLVIEWLINE(".rgListClass{")
 HTMLVIEWLINE(" list-style-type:none;")
 HTMLVIEWLINE(" margin:0px;")
 HTMLVIEWLINE(" padding:0px;")
 HTMLVIEWLINE("}")

 HTMLVIEWLINE(".rgListClass li a{")
 HTMLVIEWLINE(" text-decoration:none;")
 HTMLVIEWLINE(" font-weight:normal;")
 HTMLVIEWLINE(" font-size:15px;")
 HTMLVIEWLINE("}")

 HTMLVIEWLINE(".rgListClass li{")
 HTMLVIEWLINE(" padding-bottom:3px;")
 HTMLVIEWLINE("}")

 HTMLVIEWLINE(".searchClass{")
 HTMLVIEWLINE(" z-index: 100;")
 HTMLVIEWLINE(" position: absolute;")
 HTMLVIEWLINE(" right:2px;")
 HTMLVIEWLINE("}")

 HTMLVIEWLINE(".menuClass{")
 HTMLVIEWLINE(" position:relative;")
 HTMLVIEWLINE(" width:100%;")
 HTMLVIEWLINE("}")

 HTMLVIEWLINE(".menuColumn{")
 HTMLVIEWLINE(" float:left;")
 HTMLVIEWLINE(" padding-left:5px;")
 HTMLVIEWLINE("}")
 
  HTMLVIEWLINE(".rgListClass li:hover {")
 HTMLVIEWLINE(" background-color: lightblue;")
 HTMLVIEWLINE(" color: black;")
HTMLVIEWLINE("}")


 HTMLVIEWLINE("</style>")

 HTMLVIEWLINE("</head>")
 HTMLVIEWLINE("<body onLoad="+Q+"javascript:document.getElementById('autocomplete').focus();"+Q+">")
 HTMLVIEWLINE("<form name='form1' id='form1' method='post' action='symitar://HTMLView~Action=Post'>")

 HTMLVIEWLINE("<div id='tabs'>")
 HTMLVIEWLINE("<ul>")
 FOR X=1 TO CAT
  DO
   HTMLVIEWLINE("<li style='font-weight:bold;'><a href='#cat-"+FORMAT("99",X)+"'>")
   HTMLVIEWLINE(CATEGORY(X,0))
   HTMLVIEWLINE("</a></li>")
  END
 HTMLVIEWLINE("</ul>")

 HTMLVIEWLINE("<div class='searchClass'>Search:")
 HTMLVIEWLINE("<input id='autocomplete' title='Start typing a menu function' /> ")
 HTMLVIEWLINE("</div>")

 FOR X=1 TO CAT
  DO
   HTMLVIEWLINE("<div id='cat-"+FORMAT("99",X)+"' class='menuClass'>")
   FOR Y=1 TO VALUE(RGNAMES(X,0,0))
    DO
     IF RGNAMES(X,Y,1)="" THEN
      DO
       IF Y=1 THEN
        HTMLVIEWLINE("<div class='menuColumn'>")
       ELSE
        DO
         HTMLVIEWLINE("</ul>")
         HTMLVIEWLINE("</div>")
         HTMLVIEWLINE("<div class='menuColumn' style='margin-left:50px;'>")
        END
       HTMLVIEWLINE("<h2>"+RGNAMES(X,Y,0)+"</h2>")
       HTMLVIEWLINE("<ul class='rgListClass'>")
      END
     HTMLVIEWLINE("<li>")
     IF RGNAMES(X,Y,1)<>"" THEN
      DO
       HTMLVIEWLINE("<a href="+Q+"javascript:sbmtRgName('"+RGNAMES(X,Y,0)+"');"+Q+">")
       HTMLVIEWLINE(RGNAMES(X,Y,1)+"</a>")
      END
     HTMLVIEWLINE("</li>")
    END
   HTMLVIEWLINE("</ul>")
   HTMLVIEWLINE("</div>")
   HTMLVIEWLINE("</div>")
  END
 HTMLVIEWLINE("<div style='clear:both;height:10px'></div>")
 HTMLVIEWLINE("</div>")
 HTMLVIEWLINE("</form>")
 HTMLVIEWLINE("</body>")
 HTMLVIEWLINE("</html>")
 HTMLVIEWDISPLAY
 SUBNAME=""
 TMP=ENTERLINE(0)
 WHILE TMP<>"EOD"
  DO
   IF SEGMENT(TMP,1,7)="rgName=" THEN
    SUBNAME=SEGMENT(TMP,8,LENGTH(TMP))
   TMP=ENTERLINE(0)
  END
 IF SUBNAME<>"" THEN
  DO
   INITSUBROUTINE(SUBERR)
   EXECUTE(SUBNAME,SUBERR)
  END
END
